buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.1.0'
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:2.2.+'
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.1"
    id 'me.champeau.gradle.jmh' version '0.3.0'
    id "com.jfrog.bintray" version "1.6"
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'idea'

release {
    tagTemplate = 'v${version}'
    failOnPublishNeeded = false
}

afterReleaseBuild.dependsOn bintrayUpload

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'license'
apply plugin: 'java-library-distribution'
apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.bmuschko.nexus'

license {
    header file('LICENSE-HEADER')
}

jar {
    manifest {
        instruction 'Require-Capability', 'osgi.extender'
    }
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.eventsourcing'
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'eventsourcing'
        licenses = ['MPL-2.0']
        vcsUrl = 'https://github.com/eventsourcing/eventsourcing-graphql'
        version {
            name = project.version
        }
    }
}

dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'org.slf4j:slf4j-simple:1.7.18'

    // Useful utilities
    compile 'com.google.guava:guava:19.0'

    // Reflections API
    compile 'org.reflections:reflections:0.9.10'

    // Unit testing
    testCompile 'org.testng:testng:6.9.10'
    testCompile "org.mockito:mockito-core:1.10.19"

    // Remove boilerplate
    compile 'org.projectlombok:lombok:1.16.8'

    // OSGi
    compile 'org.osgi:org.osgi.core:6.0.0'
    compile 'org.osgi:org.osgi.service.cm:1.5.0'
    compile 'org.osgi:org.osgi.service.component:1.3.0'
    compile 'biz.aQute.bnd:biz.aQute.bndlib:3.1.0'

    // ES4J
    compile 'com.eventsourcing:eventsourcing-core:0.4.3'

    // Servlet
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    // GraphQL
    compile 'com.graphql-java:graphql-java:2.2.0'
    compile 'com.graphql-java:graphql-java-annotations:0.13.0'
    compile 'com.graphql-java:graphql-java-servlet:0.8.0'

    // JSON
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.7.2'

}

test.useTestNG()

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
        ipr.withXml { xmlFile ->
            // enable 'Annotation Processors'
            xmlFile.asNode().component.find {
                it.@name == 'CompilerConfiguration'
            }['annotationProcessing'][0].replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
                }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters"
}

extraArchive {
    sources false
    javadoc false
}

modifyPom {
    project {
        name 'eventsourcing-graphql'
        description 'Eventsourcing GraphQL adaptor'
        url 'https://github.com/eventsourcing/es4j-graphql'
        inceptionYear '2016'

        scm {
            url 'https://github.com/eventsourcing/es4j-graphql'
            connection 'scm:git:git://github.com/eventsourcing/es4j-graphql.git'
            developerConnection 'scm:git:git@github.com:eventsourcing/es4j-graphql.git'
        }

        licenses {
            license {
                name 'Mozilla Public License, v. 2.0'
                url 'http://mozilla.org/MPL/2.0/'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'yrashk'
                name 'Yurii Rashkovskii'
                email 'yrashk@gmail.com'
            }
        }
    }
}
